#!/usr/bin/env zsh

_fm-cmd-run() {
    local fs_view=DIR view=DIR fm_mode=command
    local hidden=false sort=false
    local rg_query rg_message
    local -a targets recent_dirs added_menu header=("FM $_fm_version (c) 2021 Domizio Demichelis")
    local -A clipboard preview
    _fm-read -tmp added_menu
    _fm-read -data preview

    _fm-read -data recent_dirs
    recent_dirs=( $PWD ${recent_dirs:#$PWD} )
    _fm-write -data recent_dirs

    _fm_start_dir=$PWD
    {
        _fm-panel-start
    } always {
        local -P res=$?
        tput rmcup
        if [[ $TRY_BLOCK_INTERRUPT -eq 0 && $res -gt 0 ]]; then
            _fm-print -red "\n\n[FM ERROR] Error $res returned from FM Panel!"
            return $res
        fi
    }
}
