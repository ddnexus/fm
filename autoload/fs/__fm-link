#!/usr/bin/env zsh

__fm-link() {
    setopt LOCAL_OPTIONS ERR_RETURN
    local -P sudo target
    local num=0 message='Targets linked here' name
    __fm-read -tmp targets
    __fm-open-dialog -green 'Create Links for Targets' targets
    {
         __fm-print -n -green "Create the Links in $PWD? (y/n)> "
        if read -q yn; then
            print
            if [[ ! -w $PWD ]] sudo=$FM_CMD_SUDO
            for target in $targets; do
                if [[ ! -e $target ]] continue
                name=$target:t && __fm-dest-name 'linked'
                $=sudo ln -s $target ./$name
                ((num+=1))
            done
            if [[ $num -gt 0 ]] view=$fs_view
        else
            __fm-print -yellow '\nSkippeed!'
        fi
    } always {
        __fm-close-dialog
    }
}
