#!/usr/bin/env zsh

fm::cat() {
    local -P file=$1
    case $file in
        *.pdf)     pdftotext   $file - | $=FM_CMD_CAT ;;
        *.zip)     unzip -l    $file   | $=FM_CMD_CAT ;;
        *.tar)     tar -tvf    $file   | $=FM_CMD_CAT ;;
        *.tar.gz)  tar -ztvf   $file   | $=FM_CMD_CAT ;;
        *.tar.xz)  tar -Jtvf   $file   | $=FM_CMD_CAT ;;
        *.tar.bz2) tar -jtvf   $file   | $=FM_CMD_CAT ;;
        *.deb) type dpkg-deb >/dev/null &&
                     dpkg-deb -I $file | $=FM_CMD_CAT ;;
        *)
            if [[ -L $file && $(file -b -- $file) == broken* ]]; then
                return 0
            elif [[ -s $file && ! -L $file && $(file -b --mime-encoding -- $file) == binary ]]; then
                fm::print -red "> Binary content <" | fmt -w $FZF_PREVIEW_COLUMNS
                print
                if [[ $FM_CMD_STAT ]] $=FM_CMD_STAT -- $file
            else
                $=FM_CMD_CAT -- $file
            fi
            ;;
    esac
}
