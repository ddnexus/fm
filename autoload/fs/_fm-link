#!/usr/bin/env zsh

_fm-link() {
    setopt LOCAL_OPTIONS ERR_RETURN
    local -P sudo target
    local num=0 message='Targets linked here' name
    _fm-read -tmp targets
    _fm-open-dialog -green 'Create Links for Targets' targets
    {
         _fm-print -n -green "Create the Links in $PWD? (y/n)> "
        if read -q yn; then
            print
            if [[ ! -w $PWD ]] sudo=$FM_CMD_SUDO
            for target in $targets; do
                if [[ ! -e $target ]] continue
                name=$target:t && _fm-dest-name 'linked'
                $=sudo ln -s $target ./$name
                ((num+=1))
            done
            if [[ $num -gt 0 ]] view=$fs_view
        else
            _fm-print -yellow '\nSkippeed!'
        fi
    } always {
        _fm-close-dialog
    }
}
