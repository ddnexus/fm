#!/usr/bin/env zsh

# Copyright (c) 2021 Domizio Demichelis
# Released under GNU3 license
# https://www.gnu.org/licenses/gpl-3.0.en.html


fm__view-item() {
	if [[ -z $FM__CMD_PAGER ]] { fm__notify "No FM__CMD_PAGER command found"; return 0 }
	local -P esc_name highlight_params
	if [[ -d $items[1] ]]; then
		fm__cd $items[1]
		query=''
	elif [[ -f $items[1] && $fm_mode != widget ]]; then
		esc_name=$( sed -E 's/([?:.%\\])/\\\1/g' <<< "${items[1]:t}") # escape for less prompt
		tput cup $(tput lines) 0
		case $view in
			GREP)
				case ${FM__CMD_CAT%% *} in
					bat)
						$=FM__CMD_RIPGREP -n --trim $rg_query $items[1] |
							{ awk -F: '{ print "-H"$1 }'; <<< $items[1] } |
								xargs $=FM__CMD_CAT
						;;
					cat)
						$=FM__CMD_RIPGREP --color always --passthru $rg_query $items[1] | $=FM__CMD_CAT
						;;
				esac
				;;
			*)
				fm__cat $items[1]
				;;
		esac |
			$=FM__CMD_PAGER --prompt="$esc_name (press h for help or q to quit)"
	fi
}
