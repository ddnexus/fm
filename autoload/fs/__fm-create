#!/usr/bin/env zsh

__fm-create() {
    setopt LOCAL_OPTIONS ERR_RETURN
    local -P label=$1 cmd=$2 sudo name yn
    local num=0 message="$label created"
    __fm-open-dialog -green "Create New $label"
    {
        if [[ ! -w $PWD ]] sudo=$FM_CMD_SUDO
        vared -p "%F{green}Enter a name for the new $label>%f " name
        if [[ -e $name ]]; then
            __fm-print -n -red "Override $name? (y/n)> "
            if read -q yn; then
                print ; $=sudo rm -rf $name
                __fm-print -red 'Overridden!'
            else
                __fm-print -yellow '\nSkipped!'
                return 1
            fi
        fi
        $=sudo $=cmd $name
        ((num+=1))
        view=$fs_view
    } always {
        __fm-close-dialog
    }
}
