#!/usr/bin/env zsh

fm__fav-accept-name() {
	local -P color=$1 wanted=$2 n=2
	name=$( sed -E 's/[^A-Za-z0-9._-]/_/g; s/^[^A-Za-z]*//' <<< $name )
	if [[ -z $name ]]; then
		fm__print -red '[FM ERROR] The Favorite name must be alpha-numeric starting with [A-Za-z]!'
		vared -p "%F{${color:1}}Type a valid name (empty to skip)>%f " name
	else
		while [[ -n $(hash -dm $name) && -e ~$name ]] { name=${name}-fav$n ; ((n+=1)) }
		if [[ $name == $wanted ]]; then
			return 0 # name picked and ok
		else
			# for some reason sleep avoids hanging on the next line the very
			# first time it's called from a fresh launched fm.
			# The above $name == $wanted seems to cause the weirdness
			sleep .1
			vared -p "%F{${color:1}}Confirm the Favorite name (empty to skip)>%f " name
		fi
	fi
	if [[ $name ]] fm__fav-accept-name $color $name
}
