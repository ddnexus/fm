#!/usr/bin/env zsh

fm__cmd-run() {
    local fs_view=DIR view=DIR fm_mode=command
    local hidden=false sort=false
    local rg_query rg_message
    local -a targets recent_dirs added_menu header=("FM $fm__version (c) 2021 Domizio Demichelis")
    local -A clipboard preview
    fm__read -tmp added_menu
    fm__read -data preview

    fm__read -data recent_dirs
    recent_dirs=( $PWD ${recent_dirs:#$PWD} )
    fm__write -data recent_dirs

    fm__start_dir=$PWD
    {
        fm__panel-start
    } always {
        local -P res=$?
        tput rmcup
        if [[ $TRY_BLOCK_INTERRUPT -eq 0 && $res -gt 0 ]]; then
            fm__print -red "\n\n[FM ERROR] Error $res returned from FM Panel!"
            return $res
        fi
    }
}
