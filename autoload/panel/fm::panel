#!/usr/bin/env zsh

fm::panel() {
    local query key items=()
    local -P n=0 expect res

    fm::view-${view:l} |
        fzf ${(P)=${:-FM_${(U)fm_mode}_FZF_OPTS}} \
                --prompt="$(fm::prompt)" \
                --expect=${(Pj|,|)${:-__fm_${fm_mode}_keys}} \
                --preview="$fm_root/preview/panel {} $view $hidden $rg_query" \
                --bind="${FM_KEY_SELECT_ALL}:select-all" \
                --preview-window=$FM_PREVIEW_MODES[$preview[${fm_mode}-panel]] \
                --print-query \
                --ansi \
                --multi \
                --no-clear \
                $@ |
            local lines=( "${(@f)$(</dev/stdin)}" )

    res=$?; if [[ $res -gt 1 ]] return $res # 1 fzf no match
    query=$lines[1]
    key=$lines[2]
    lines=( ${lines:2} )

    header=()
    sort=false
    fm::set-items

    if [[ $key == $FM_KEY_MENU ]] fm::menu

    case $key in
    # FM UI
        $FM_KEY_REFRESH|REFRESH|MENU|'')     : ;; # explicit refresh / MENU toggles back from menu panel / no key
        $FM_KEY_PREVIEW|PREVIEW)             fm::cycle-preview ${fm_mode}-panel ;;
        #FM_KEY_SELECT_ALL is a bind key
        $FM_KEY_NOTIFY_WD|NOTIFY_WD)         fm::notify "Working Dir: $PWD" ;;
        $FM_KEY_HIDDEN|HIDDEN)               fm::toggle-hidden ;;
        $FM_KEY_SORT|SORT)                   fm::sort-items ;;
        esc|ctrl-c|ctrl-z|$FM_KEY_QUIT|QUIT) return 0 ;;
    # FM Views
        double-click|$FM_KEY_VIEW_ITEM|VIEW_ITEM) fm::view-item ;;
        $FM_KEY_VIEW_DIR|VIEW_DIR)           view=DIR;  fs_view=DIR ;;
        $FM_KEY_VIEW_FIND|VIEW_FIND)         view=FIND; fs_view=FIND ;;
        $FM_KEY_VIEW_GREP|VIEW_GREP)         fm::set-grep ;;
        $FM_KEY_VIEW_REC|VIEW_REC)           if [[ $view != REC ]] { view=REC } else { view=$fs_view } ;;
        $FM_KEY_VIEW_FAV|VIEW_FAV)           if [[ $view != FAV ]] { view=FAV } else { view=$fs_view } ;;
    # Navigation
        $FM_KEY_CD_HOME|CD_HOME)             fm::cd $HOME ;;
        $FM_KEY_CD_ROOT|CD_ROOT)             fm::cd / ;;
        $FM_KEY_CD_PARENT|CD_PARENT)         fm::cd .. ;;
        $FM_KEY_CD_LAST|CD_LAST)             fm::cd $OLDPWD ;;
        $FM_KEY_CD_CONTAIN|CD_CONTAIN)       fm::cd ${items[1]:h} ;;
        $FM_KEY_CD_START|CD_START)           fm::cd $__fm_start_dir ;;
    # FS Commands
        $FM_KEY_COPY|COPY)                   fm::select copied ;;
        $FM_KEY_CUT|CUT)                     fm::select cut ;;
        $FM_KEY_PASTE|PASTE)                 fm::paste ;;
        $FM_KEY_NEW_FILE|NEW_FILE)           fm::create File touch ;;
        $FM_KEY_NEW_DIR|NEW_DIR)             fm::create Dir mkdir ;;
        $FM_KEY_FAV_ADD_WD|FAV_ADD_WD)       fm::fav-add $PWD ;;
        $FM_KEY_FAV_ADD|FAV_ADD)             fm::fav-add ;;
        $FM_KEY_MARK|MARK)                   fm::mark ;;
        $FM_KEY_LINK|LINK)                   fm::link ;;
        $FM_KEY_RENAME|RENAME)               if [[ $view == FAV ]] { fm::fav-rename } else { fm::rename } ;;
        $FM_KEY_REMOVE|REMOVE)               if [[ $view == FAV ]] { fm::fav-remove } else { fm::remove } ;;
        $FM_KEY_EXEC_ON|EXEC_ON)             fm::switch-x ON ;;
        $FM_KEY_EXEC_OFF|EXEC_OFF)           fm::switch-x OFF ;;
    # Tools
        $FM_KEY_EDIT|EDIT)                   fm::edit ;;
        $FM_KEY_OPEN|OPEN)                   fm::open ;;
        $FM_KEY_NEW_SHELL|NEW_SHELL)         fm::new-shell ;;
        $FM_KEY_COPY_PATH|COPY_PATH)         fm::copy-paths ;;
    # Widget
        $FM_KEY_PICK|PICK)                   print -- "${(Dq@)items}"; return 0 ;; # widget only
        $FM_KEY_PICK_WD|PICK_WD)             print -- "${(Dq)PWD}"; return 0 ;; # widget only
    # Added Tools
        *)                                   fm::run-added ;;
    esac

    fm::panel-start
}
