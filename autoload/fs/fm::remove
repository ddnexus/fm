#!/usr/bin/env zsh

fm::remove() {
    setopt LOCAL_OPTIONS ERR_RETURN
    local -P item sudo name yn
    local num=0 message='Items removed'
    fm::open-dialog -red 'Remove Selected Items' items
    {
        for item in $items; do
            name=$item:t
            if [[ -w $item:h ]] { sudo='' } else { sudo=$FM_CMD_SUDO }
            fm::print -n -red "Remove $name? (y/n)> "
            if read -q yn; then
                print ; $=sudo rm -rf $item
                ((num+=1))
                fm::print -red 'Removed!'
            else
                fm::print -yellow '\nSkippeed!'
                continue
            fi
        done
    } always {
        fm::close-dialog
    }
}
