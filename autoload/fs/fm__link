#!/usr/bin/env zsh

fm__link() {
    setopt LOCAL_OPTIONS ERR_RETURN
    local -P sudo target
    local num=0 message='Targets linked here' name
    fm__read -tmp targets
    fm__open-dialog -green 'Create Links for Targets' targets
    {
         fm__print -n -green "Create the Links in $PWD? (y/n)> "
        if read -q yn; then
            print
            if [[ ! -w $PWD ]] sudo=$FM__CMD_SUDO
            for target in $targets; do
                if [[ ! -e $target ]] continue
                name=$target:t && fm__dest-name 'linked'
                $=sudo ln -s $target ./$name
                ((num+=1))
            done
            if [[ $num -gt 0 ]] view=$fs_view
        else
            fm__print -yellow '\nSkippeed!'
        fi
    } always {
        fm__close-dialog
    }
}
